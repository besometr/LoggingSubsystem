////////////////////////////////////////////////////////////////////////////////
// Подсистема "Логирование"
//
// 
// 
////////////////////////////////////////////////////////////////////////////////



#Область ОбработчикиСобытийФормы


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПолучитьСписокИндексовНаСервере();
КонецПроцедуры


#КонецОбласти



#Область ОбработчикиСобытийЭлементовШапкиФормы
//Код процедур и функций
#КонецОбласти



#Область ОбработчикиСобытийЭлементовТаблицыФормы_ИмяТаблицыФормы
//Код процедур и функций
#КонецОбласти



#Область ОбработчикиКомандФормы


&НаКлиенте
Процедура ПолучитьСписокИндексов(Команда)
	ПолучитьСписокИндексовНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПолучитьСписокИндексовНаСервере()

	СтруктураОтвета = ЛогированиеСервер.ВыполнитьПолучениеИндексовСервер();
	ЗаполнитьДанныеФормыНаОсновеРезультатаЗапрос(СтруктураОтвета)
	
КонецПроцедуры


&НаКлиенте
Процедура СохранитьНастройкиПодключения(Команда)
	СохранитьНастройкиПодключенияНаСервере();
КонецПроцедуры


&НаСервере
Процедура СохранитьНастройкиПодключенияНаСервере()
	
	Константы.ELK_Индекс.Установить(Индекс); 
	Константы.ELK_Тип.Установить(Тип);
	Константы.ELK_Сервер.Установить(Сервер);
	Константы.ELK_Порт.Установить(Порт);
	Константы.ELK_Таймаут.Установить(Таймаут);
	
	ПараметрыСеанса.ПараметровПодключенияКELK = ЛогированиеСервер.КонструкторСтруктурыПараметровПодключенияКELK();
	
КонецПроцедуры



#КонецОбласти



#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДанныеФормыПоУмолчанию()
	
	СтруктураПараметровПодключения = ПараметрыСеанса.ПараметровПодключенияКELK;
	Индекс				= СтруктураПараметровПодключения.Индекс;
	Тип					= СтруктураПараметровПодключения.Тип;
	Сервер				= СтруктураПараметровПодключения.Сервер;
	Порт				= СтруктураПараметровПодключения.Порт;
	Таймаут				= СтруктураПараметровПодключения.Таймаут;
	
	ИмяКластера = "";
	Статус		= "";
	тзИндексы.Очистить();
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьДанныеФормыНаОсновеРезультатаЗапрос(СтруктураОтвета)
	
	ЗаполнитьДанныеФормыПоУмолчанию();
	
	Если Не (ТипЗнч(СтруктураОтвета) = Тип("ФиксированнаяСтруктура") И ТипЗнч(СтруктураОтвета.РезультатВыполнения) = Тип("Соответствие")) Тогда 
		Сообщить("Не удалось получить параметры индексов по причине: 
					| " + СтруктураОтвета.ТекстОшибки);
		Возврат
	КонецЕсли;
	
	СтруктураКластера = СтруктураОтвета.РезультатВыполнения;
	
	ИмяКластера	= СтруктураКластера["cluster_name"];
	Статус		= СтруктураКластера["status"];
	
	СоответствиеИндексов = СтруктураКластера["indices"];	
	
	Если Не ТипЗнч(СоответствиеИндексов) = Тип("Соответствие") Тогда 
		Возврат
	КонецЕсли;
	
	Для Каждого КлючИЗначение Из СоответствиеИндексов Цикл 
		
		нСтрока = тзИндексы.Добавить();
		
		нСтрока.Индекс = КлючИЗначение.Ключ;
		СоответствиеПараметровИндекса = КлючИЗначение.Значение;
		
		нСтрока.Статус				= СоответствиеПараметровИндекса["status"];
		нСтрока.КоличествоШардов	= СоответствиеПараметровИндекса["number_of_shards"];
		нСтрока.КоличествоРеплик	= СоответствиеПараметровИндекса["number_of_replicas"];
		
	КонецЦикла;
//indices	
КонецПроцедуры




#КонецОбласти







